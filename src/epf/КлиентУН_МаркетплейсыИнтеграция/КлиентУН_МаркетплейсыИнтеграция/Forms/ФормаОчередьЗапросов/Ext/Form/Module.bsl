
&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ОбновитьОчередьЗапросовСервисаУН" И Источник = ВладелецФормы Тогда
		ОбновитьОчередьЗапросовСервисаУН();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьОчередьЗапросовСервисаУН() Экспорт
	
	ИдентификаторЗапросаТекущейСтроки = Неопределено;
	ТекущиеДанные = Элементы.ТекущиеЗапросы.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		ИдентификаторЗапросаТекущейСтроки = ТекущиеДанные.Идентификатор;
	КонецЕсли;
	
	ТекущиеЗапросы.Очистить();
	
	ТекущиеЗапросыСервиса = ВладелецФормы.ТекущиеЗапросыСервисаУН();
	
	ЗаполнитьЗначенияСвойств(
		ЭтотОбъект,
		ТекущиеЗапросыСервиса,
		"ОжиданиеРезультатов,
		|ПериодОпросаРезультата,
		|ПериодХраненияРезультата,
		|ПериодХраненияОшибок"
	);
	
	СтрокаДляАктивации = Неопределено;
	Для Каждого Запрос Из ТекущиеЗапросыСервиса.Запросы Цикл
		
		НовСтр = ТекущиеЗапросы.Добавить();
		ЗаполнитьЗначенияСвойств(НовСтр, Запрос,, "Обработчик");
		НовСтр.Обработчик = Запрос.Обработчик.ИмяПроцедуры;
		
		Если ЗначениеЗаполнено(ИдентификаторЗапросаТекущейСтроки)
			И НовСтр.Идентификатор = ИдентификаторЗапросаТекущейСтроки Тогда
			СтрокаДляАктивации = НовСтр.ПолучитьИдентификатор();
		КонецЕсли;
		
	КонецЦикла;
	
	Если ЗначениеЗаполнено(СтрокаДляАктивации) Тогда
		Элементы.ТекущиеЗапросы.ТекущаяСтрока = СтрокаДляАктивации;
	КонецЕсли;
	
КонецПроцедуры
